//
//     Politics - Program to map TSE data (csv) to RDF triples using the MapDSL language.
//     Copyright (C) 2018 Dilvan A. Moreira.
//     dilvan@gmail.com
//
//     This program is free software: you can redistribute it and/or modify
//     it under the terms of the GNU General Public License as published by
//     the Free Software Foundation, either version 3 of the License, or
//     (at your option) any later version.
//
//     This program is distributed in the hope that it will be useful,
//     but WITHOUT ANY WARRANTY// without even the implied warranty of
//     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//     GNU General Public License for more details.
//
//     You should have received a copy of the GNU General Public License
//     along with this program.  If not, see <https://www.gnu.org/licenses/>.
//

// Mapeamento das planilhas "votacao_candidato_munzona_2014"
csv {
    delimiter   ";"
    charset     "ISO-8859-1"
    header      "dataGeracao;horaGeracao;anoEleicao;numTurno;descricaoEleicao;siglaUF;siglaUE;codigoMunicipio;nomeMunicipio;numeroZona;codigoCargo;numeroCand;sqCandidato;nomeCandidato;nomeUrnaCandidato;descricaoCargo;codSitCandSuperior;descSitCandSuperior;codigoSitCandidato;descSitCandidato;codigoSitCandTot;descSitCandTot;numeroPartido;siglaPartido;nomePartido;sequencialLegenda;nomeColigacao;composicaoLegenda;totalVotos;transito"
    null        "#NULO#"
}


type csv.numeroZona,      integer
type csv.totalVotos,      integer
type csv.numeroCand,      integer
type csv.sqCandidato,     integer
type csv.codigoMunicipio, integer


prefix pol,     "http://purl.org/onto/politics#"
prefix prop,    "http://dbpedia.org/property/"
prefix dbr,     "http://dbpedia.org/resource/"
prefix dbo,     "http://dbpedia.org/ontology/"
prefix foaf,    "http://xmlns.com/foaf/0.1/"
prefix wd,      "http://www.wikidata.org/entity/"


base "http://purl.org/politics/id/"

uri stateURI,               "state",            csv.'siglaUF'
uri municipalityURI,        "municipality",     csv.'codigoMunicipio'
uri electionURI,            csv.'anoEleicao',   "election"
uri turnURI,                csv.'anoEleicao',   "turn",                 csv.'numTurno'
uri candidacyURI,           csv.'anoEleicao',   csv.'siglaUE',          "candidacy",            csv.'sqCandidato'
uri electoralDistrictURI,   csv.'anoEleicao',   "electoralDistrict",    csv.'codigoMunicipio',  csv.'numeroZona'
uri districtVotingURI,      csv.'anoEleicao',   "districtVoting",       csv.'codigoMunicipio',  csv.'numeroZona',   csv.'sqCandidato'

// Candidacy (Candidatura)
map (candidacy, candidacyURI) {
    pol.situation   csv.descSitCandTot //Diz se foi pro 2 turno ou não e se foi eleito
}


// Turn (Turno)
map(turn, turnURI)


// Election (Eleição)
map(election, electionURI)


// State (Estado)
map(state, stateURI) {
    rdfs.label          csv.siglaUF
    rdf.type            pol.state
    pol.abbreviation    csv.siglaUF
    // Coutry, adicionar em alguma planilha que apareça wd.P17
}


// Municipality (Município)
map(municipality, municipalityURI) {
    pol.state   state
    rdfs.label  csv.nomeMunicipio
    pol.tseCode csv.codigoMunicipio
    rdf.type    pol.municipality
}


// Electoral District (Distrito Eleitoral)
map(electoralDistrict, electoralDistrictURI) {
    pol.turn            turn
    pol.election        election
    pol.municipality    municipality
    pol.number          csv.numeroZona
    rdf.type            pol.electoralDistrict
}


// District Voting (Votação na Zona Eleitoral)
map(districtVoting, districtVotingURI) {
    rdfs.label              csv.nomeUrnaCandidato
    pol.electoralDistrict   electoralDistrict
    pol.candidacy           candidacy
    pol.turn                turn
    pol.votes               csv.totalVotos
    rdf.type                pol.districtVoting
}
